Package k31.grc;


Helpers

	letter = [['a' .. 'z'] + ['A' .. 'Z']];
	digit = ['0' .. '9'];
	underscore = '_';
	
	tab = 9;
	cr = 13;
	lf = 10;
	
	eol = cr | lf | cr lf;
	whitespace = ' ' | tab | eol;
		
	printchar = [32 .. 126];
	prtabchar = [printchar + tab];
	
	specialchar = [[''' + '"'] + '\'];
	commonchar = [printchar - specialchar];
	
	hexdigit = digit | ['a' .. 'f'] | ['A' .. 'F'];
	escape = '\n' | '\t' | '\r' | '\e' | '\\' | '\' ''' | '\"' | '\x' hexdigit hexdigit;
	

States

	code, comment, comment_possible_end;

	
Tokens
	
// keywords

	{code} key_and = 'and';
	{code} key_char = 'char';
	{code} key_div = 'div';
	{code} key_do = 'do';
	{code} key_else = 'else';
	{code} key_fun = 'fun';
	{code} key_if = 'if';
	{code} key_int = 'int';
	{code} key_mod = 'mod';
	{code} key_not = 'not';
	{code} key_nothing = 'nothing';
	{code} key_or = 'or';
	{code} key_ref = 'ref';
	{code} key_return = 'return';
	{code} key_then = 'then';
	{code} key_var = 'var';
	{code} key_while = 'while';


// main

	{code} identifier = letter (letter | digit | underscore)*;

	{code} const_int = digit+;
	{code} const_char = ''' (commonchar | escape) ''';
	{code} const_string = '"' (commonchar | escape)* '"';
	
// operators

	{code} oper_plus = '+';
	{code} oper_minus = '-';
	{code} oper_mult = '*';
	{code} oper_div = '/';
	{code} oper_hash = '#';
	{code} oper_eq = '=';
	{code} oper_ne = '<>';
	{code} oper_lt = '<';
	{code} oper_gt = '>';
	{code} oper_le = '<=';
	{code} oper_ge = '>=';


// separators

	{code} sep_lpar = '(';
	{code} sep_rpar = ')';
	{code} sep_lbrack = '[';
	{code} sep_rbrack = ']';
	{code} sep_lbrace = '{';
	{code} sep_rbrace = '}';


// ignored

	// whitespace
	{code} whitespace = (whitespace)*;

	// comment line
	{code} comment_line = '$' ( [prtabchar - '$'] prtabchar* )? eol?;
		
	// comment span
	{code -> comment} comment_start = '$$';
	{comment_possible_end -> code} comment_end = '$'; // priority over next token
	{comment -> comment_possible_end, comment_possible_end} comment_content = ([prtabchar - '$'] | eol)* '$'?;


Ignored Tokens

	whitespace, comment_line, comment_start, comment_content, comment_end;
